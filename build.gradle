plugins {
	id "com.github.hierynomus.license" version "0.14.0" apply false
}

allprojects {
	group = 'com.nortal.cas'
	version = '5.1.1-1'
	repositories {
		jcenter()
		maven {
            url 'https://artifact.nortal.com/nortal-libs-release-local'
        }
	}
}

subprojects {
	apply plugin: 'maven-publish'
	
	publishing {
		if (!project.hasProperty('bintrayUsername')) {
    		ext.bintrayUsername = ''
  		}
  	
	  	if (!project.hasProperty('bintrayPassword')) {
	    	ext.bintrayPassword = ''
	  	}

	  	repositories {
	        maven {
	        	url 'https://api.bintray.com/maven/nortal/cas-modules/'+project.name
	            credentials {
	                username bintrayUsername
	                password bintrayPassword
	            }
	        }
	    }
	}
}

configure(subprojects.findAll {it.name.contains 'support'}) {
	apply plugin: 'java'
	apply plugin: 'com.github.hierynomus.license'
	
	license {
	      header rootProject.file('LICENSE_HEADER')
	      strictCheck true
	}


	task sourceJar(type: Jar) {
    	from sourceSets.main.allJava
	}
	
	publishing {
	    publications {
	        maven(MavenPublication) {
	            from components.java
	
	            artifact sourceJar {
	                classifier "sources"
	            }
	        }
	    }
	}
}
